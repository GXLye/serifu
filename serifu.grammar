@top Doc { (Page | Spread)+ }

Page {
	PageToken Panel*
}

Spread { 
	SpreadToken Panel*
}

Panel {
	PanelToken Line*
}

Line {
	(Text | Note | Sfx)
}

Sfx {
	Star SfxTranslation SfxSource? newlines
}

Note {
	NoteToken content newlines
}

Text {
	Source ("/" Style)? ":" content newlines
}

Source {
	word
}

Style {
	word
}

content {
	( unstyledText | Bold | BoldItal | Ital | Star | Colon | DoubleStar | Underscore )* |	BlockText
}

unstyledText {
	anyText
}

@skip { spaces }

@tokens {
	spaces { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+ }
	newlines { $[\r\n\u2028\u2029]+ }

	PageToken { "#" ![\n#]* newlines}
	SpreadToken { "##" ![\n]* newlines}
	@precedence { SpreadToken, PageToken }

	PanelToken { "-" ![\n]* newlines}

	wordChar { std.asciiLetter | $[_$\u{a1}-\u{10ffff}] | std.digit | "-" | "'" | "&" | " "}
	word { (std.asciiLetter | $[\u{a1}-\u{10ffff}] | std.digit) (wordChar* (std.asciiLetter | $[\u{a1}-\u{10ffff}] | std.digit))? }


	anychar { ![\u0009:*_\u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff\r\n\u2028\u2029] }
	anyText { anychar+ }

	Star { "*" }
	Bold {
		Star anychar* Star
	}

	DoubleStar { Star Star }
	BoldItal {
		DoubleStar anychar* DoubleStar
	}

	Underscore { "_" }
	Ital { Underscore anychar* Underscore }

	Colon { ":" }

	NoteToken { "!" }
	SfxTranslation { ![\n)(]+ }
	SfxSource { "(" ![\n)(]+ ")"}

	BlockText { "/=" BlockTextRest }
	BlockTextRest { ![=] BlockTextRest | "=" BlockTextAfterEquals}
	BlockTextAfterEquals { "/" | "=" BlockTextAfterEquals | ![/=] BlockTextRest }

	@precedence { BlockText, SpreadToken, PageToken, PanelToken, SfxSource, SfxTranslation, BoldItal, DoubleStar, Star, Bold, Underscore, Ital, anyText, word, spaces }
}